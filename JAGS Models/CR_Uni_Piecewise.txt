model {
  for(i in 1:n) {
    for(k in 1:Nrisks) {
        for(j in 1:int.obs[i]) {
       cond[i, j, k] <- step(time[i] - a[j + 1])
       HH[i, j, k] <- cond[i, j, k] * (a[j + 1] - a[j]) * lambda[j, k] + (1 - cond[i, j, k]) * (time[i] - a[j]) * lambda[j, k]
      }
    # Cumulative hazard function
    H[i, k] <- sum(HH[i, 1:int.obs[i], k])
    }
 }

  for(i in 1:n) {
    for(k in 1:Nrisks) {
       elinpred[i, k] <- exp(inprod(beta[k], X[i, ]))
       # Log-hazard functions
       logHaz[i, k] <- log(lambda[int.obs[i], k] * elinpred[i, k])
       # Log-survival functions
       logSurv[i, k] <- -H[i, k] * elinpred[i, k]
  }

  # Definition of the log-likelihood using zeros trick
  phi[i] <- 100000 - inprod(delta[i, ], logHaz[i, ]) - sum(logSurv[i, ])
  zeros[i] ~ dpois(phi[i])
  }

  # Prior distributions
  for(k in 1:Nrisks) {
      beta[k] ~ dnorm(0, 0.1)
      for(w in 1:m) {
        lambda[w, k] ~ dgamma(0.01, 0.01)
  }
 }
  psi ~ dgamma(0.01, 0.01)
}